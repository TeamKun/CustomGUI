buildscript {
    ext.kotlin_version = '1.4.32'
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}
apply plugin: "kotlin"
apply plugin: "kotlinx-serialization"
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

version = '1.7'
group = 'kotx.minecraft.mod'
archivesBaseName = 'CustomGUI'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings channel: 'official', version: '1.16.5'
    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                customgui {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                customgui {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                customgui {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    jcenter()
}


dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.0'
    embed group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: "1.4.32"
    embed group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "1.4.32"
    embed group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: "1.4.32"

    embed "io.insert-koin:koin-core:3.0.1-beta-2"
    embed "io.insert-koin:koin-core-ext:3.0.1-beta-2"

    embed "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"

    embed "io.ktor:ktor-client-core:1.5.3"
    embed "io.ktor:ktor-client-okhttp:1.5.3"

    embed "com.fasterxml.jackson.core:jackson-databind:2.12.2"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "customgui",
                "Specification-Vendor"    : "kotx",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${archiveVersion}",
                "Implementation-Vendor"   : "kotx",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
